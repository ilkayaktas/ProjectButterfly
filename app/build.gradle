apply plugin: 'com.android.application'
apply plugin: 'realm-android'

def gitTag = 'git describe --tags'.execute([], project.rootDir).text.trim()
def gitCommitCount = 100 + Integer.parseInt(
        'git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

def getApiKey() {
    def apiKeyFile = file('secret.properties')
    if (apiKeyFile.canRead()) {
        def Properties props = new Properties()
        props.load(new FileInputStream(apiKeyFile))
        return props['API_KEY']
    } else {
        throw new GradleException("Missing api key. Could not read from secret.properties!")
    }
}

android {
    signingConfigs {
        config {
            keyAlias 'projectname'
            keyPassword '21122784'
            storeFile file('../projectname.jks')
            storePassword '21122784'
        }
    }


    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.ilkayaktas.projectname"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode gitCommitCount
        versionName gitTag
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        android.defaultConfig.vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"

            buildConfigField "String", "ADMOB_APP_ID", "\"ca-app-pub-8425622277008316~9226494899\""
            resValue "string", "ADMOB_BANNER_AD_UNIT_ID", "\"/6499/example/banner\""
            buildConfigField "String", "ADMOB_BANNER_AD_UNIT_ID", "\"/6499/example/banner\""
            buildConfigField "String", "ADMOB_INTERSTITIAL_AD_UNIT_ID", "\"/6499/example/interstitial\""
            buildConfigField "String", "ADMOB_REWARDEDVIDEO_AD_UNIT_ID", "\"ca-app-pub-3940256099942544/5224354917\""
            buildConfigField "String", "ADMOB_NATIVE_AD_UNIT_ID", "\"ca-app-pub-3940256099942544/2247696110\""
        }
        release {
            debuggable false
            applicationIdSuffix ".release"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "String", "ADMOB_APP_ID", "\"ca-app-pub-8425622277008316~9226494899\""
            resValue "string", "ADMOB_BANNER_AD_UNIT_ID", "\"ca-app-pub-8425622277008316/603656823\""
            buildConfigField "String", "ADMOB_BANNER_AD_UNIT_ID", "\"ca-app-pub-8425622277008316/603656823\""
            buildConfigField "String", "ADMOB_INTERSTITIAL_AD_UNIT_ID", "\"ca-app-pub-8425622277008316/8201968080\""
            buildConfigField "String", "ADMOB_REWARDEDVIDEO_AD_UNIT_ID", "\"ca-app-pub-8425622277008316/9544584319\""
            buildConfigField "String", "ADMOB_NATIVE_AD_UNIT_ID", "\"ca-app-pub-3940256099942544/2247696110\""
        }


        applicationVariants.all {
            variant -> variant.buildConfigField "String", "API_KEY", "\"" + getApiKey() + "\""
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile project(path: ':annotationprocessing')
    annotationProcessor project(':annotationprocessing')

    // Appcompat and Constraint Layout
    compile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support.constraint:constraint-layout:$rootProject.constraintsLibraryVersion" // Constraints layout
    compile "com.android.support:design:$rootProject.supportLibraryVersion" // snackbar
    compile "com.android.support:cardview-v7:$rootProject.supportLibraryVersion" // Cardview

    // Ads
    compile "com.google.android.gms:play-services-ads:$rootProject.adsVersion"

    // Test
    testCompile "junit:junit:$rootProject.junitVersion"
    androidTestCompile("com.android.support.test.espresso:espresso-core:$rootProject.espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs'
    })

    // Dagger
    compile "com.google.dagger:dagger:$rootProject.daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.daggerVersion"
    provided 'javax.annotation:jsr250-api:1.0'

    // Butterknife
    compile "com.jakewharton:butterknife:$rootProject.butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butterKnifeVersion"

    // EventBus
    compile "org.greenrobot:eventbus:$rootProject.eventBusVersion"

    // Lovely Dialog
    compile "com.yarolegovich:lovely-dialog:$rootProject.lovelydialog"

    // Toasty
    compile "com.github.GrenderG:Toasty:$rootProject.toastyVersion"

    // Requery
    compile 'io.requery:requery-android:1.4.1' // for android
    annotationProcessor 'io.requery:requery-processor:1.4.1'
}
